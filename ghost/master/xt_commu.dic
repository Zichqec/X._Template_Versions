OnCommunicate
{
	if reference0 == "user" || reference0 == "User"
 	{
		//Answering the user - I've removed the function that goes along with this because it doesn't link to anything.
	}
 	else
	{
 		//Answering another ghost
		ReplyToGhost
 	}
}

//This is your ghost starting a conversation with another ghost. It will choose a random one from the currently open ghosts.
OnStartTalking
{
	AlreadyResponded = 1
	if ghostexcount > 0
	{
		_num = RAND(ghostexcount)
		_randghost = "%(ghostexlist[_num])"
		--
		//Checks for the Sakura name
		case _randghost
		{
			when "Girl" //Known ghosts, add as many whens as needed.
			{
				res_reference0 = "Girl"
				
				"\0\s[0]Hello Girl!\w8\w8 Hello Triangle!\w8\w8\e GirlTriangleHello01"
				"\0\s[0]Hi Girl!\w8\w8 How are you,\w4 Triangle?\w8\w8\e GirlTriangleHello03"
			}
			others //Ghosts there is no specific response to.
			{
				res_reference0 = _randghost
				
				"\0\s[0]Hello %(_randghost)!\w8\w8\e"
			}
		}
	}
	else //If no ghosts are open.
	{
		"\0\s[0]Nobody else is here.\e"
	}
}

ReplyToGhost //This is your ghost receiving a communication.
{
	_Speech = SPLIT(reference1,"\e")[0] //Everything the ghost said that the user can see
	ConvoTag = SPLIT(reference1,"\e")[1] //Everything after the \e, which will be the invisible tags that track the conversation
	_Last = SPLIT(reference1,"::")[1] //Ghosts using the reflect system send ":%(_Speech)" at the end of every communication, so _Last is what THIS ghost last said. See below.
	
	_Last = REPLACE(_Last,"\![quicksection,true]","") //Removing the quicksection tags or they overlap and break...
	_Last = REPLACE(_Last,"\![quicksection,false]","")
	_Last = REPLACE(_Last,"\w1","")
	_Last = REPLACE(_Last,"\w4","")
	_Last = REPLACE(_Last,"\w8","")
	//You'll need to add any weight tags that you use in dialogue here so they'll be removed. I only use these ones.
	
	case reference0
	{
		when "Girl"
		{
			"\![quicksection,true]%(_Last)\![quicksection,false]" //Only add this for ghosts that use the reflect system
			--
			"%(TalkToGirl)"
			--
			"::%(_Speech)" //Reflects back what the other ghost said, only put it here if you're talking to a ghost that uses the same system or things may break
		}
		others //Responses to unknown ghosts. AlreadyResponded keeps them from getting stuck in a loop.
		{
			if AlreadyResponded == 0
			{
				AlreadyResponded = 1
				res_reference0 = reference0
				
				"\0\s[0]Hey there %(reference0)!\w8\w8\e"
			}
			else
			{
				AlreadyResponded = 0
				res_reference0 = ""
				
				"Hope you're doing well.\e"
			}
		}
	}
	
}

TalkToGirl //All responses to Girl would be here. Make one of these for each ghost they talk to so you can keep them separate easily! Or you can put it all in the function above, whatever works for you.
{
	if "GirlTriangleHello02" _in_ ConvoTag
	{
		res_reference0 = "Girl"
		
		"\0\s[0]We'll never see this dialogue.\w8\w8 Oh well,\w4 it's here for example!\w8\w8\e"
	}
	elseif "GirlTriangleHello04" _in_ ConvoTag
	{
		res_reference0 = ""
		
		"\0\s[0]Another example response!\e"
	}
	else //You should have an else just in case the other ghost isn't updated with responses
	{
		res_reference0 = ""
		
		"\0\s[0]Hi Girl!\e"
	}
}