//I put anything related to who the user is in here. Name, pronouns, birthday.

OnChangeName
{
	"\0\s[0]Ok,\w4 what should I call you?"
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnNameTeach]\*\![open,inputbox,OnNameTeach,-1]\e"
}

OnNameTeach
{	
	_tempname = CUTSPACE(reference.raw[0]) //this stores the original name in _tempname
	_name = reference.raw[0] //putting the raw name into _name to do some tests on it
	_name = TOLOWER(_name) //changes it to lowercase
	_name = REPLACE(_name," ","") //removes spaces
	_name = REPLACE(_name,".","") //removes periods
	_name = REPLACE(_name,"-","") //removes dashes
	
	_is_first_boot = 0; if reference2 == "firstboot"; _is_first_boot = 1
	
	if _is_first_boot; "\_l[-10000,-10000]\q[ ,__VERY_FIRST_BOOT__]\_l[0,0]\*\t" //Prevents balloon from timing out if this is during first boot. Also prevents the user from closing out the input box
	--
	if _tempname == username && !_is_first_boot //If the user gives the exact same name as before. The firstboot check has to be there in case they put in 'user'
	{
		"\0\s[0]Oh,\w4 did you change your mind?"
	}
	elseif _name == "xtemplate" || TOLOWER(_tempname) == "x." //Checking with _tempname because 'X.' has a period, which would be removed. I don't want to stop someone putting in just X
	{
		"\0\s[0]That's my name!\w8\w8 I can't call you that!\w8\w8 Try again."
		--
		EnterNameAgain
	}
	elseif _name == "" //If the user doesn't put in anything
	{
		username = "User"
		
		"\0\s[0]Oh,\w4 ok,\w4 I'll just call you User,\w4 then.\w8\w8 Let me know if you want to change it!"
	}
	else //if it's none of these above names, we can use it. You can add names that just have comments and can still be used, just don't add a \e at the end.
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0]Ok,\w4 I'll call you %(username) then."
	}
	--
	if !_is_first_boot //If the user was changing their name through the config menu
	{
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //This is continuing from FirstBoot
	{
		"\w8\w8 Good to meet you!\w8\w8 And how should I refer to you?"
		--
		all:/
		{
			"\n\n\t\*"
			"\![*]\q[He/Him/His,OnPronouns,masc,firstboot]\n"
			"\![*]\q[She/Her/Hers,OnPronouns,fem,firstboot]\n"
			"\![*]\q[They/Them/Their,OnPronouns,undef,firstboot]"
		}
	}
}

//If the user put in an invalid name
EnterNameAgain
{
	_is_first_boot = 0; if reference2 == "firstboot"; _is_first_boot = 1
	_function = "OnNameTeach"; if _is_first_boot; _function = "OnFirstNameTeach"
	
	if !_is_first_boot; "\n\n\![*]\q[Cancel,OnConfigMenu,OnNameTeach]" //If changing name through the menu
	--
	"\![open,inputbox,OnNameTeach,-1,--reference=firstboot]\e"
}

//Envelope names are the same as the GT template, since I figured most people will be used to them.
OnChangePronoun
{
	"\0\s[0]How should I refer to you?"
	--
	all:/
	{
		"\n\n\_q"
		"\![*]\q[He/Him/His,OnPronouns,masc]\n"
		"\![*]\q[She/Her/Hers,OnPronouns,fem]\n"
		"\![*]\q[They/Them/Their,OnPronouns,undef]\n\n"
		"\![*]\q[Cancel,OnConfigMenu]\e"
	}
}


OnPronouns
{
	pronouns = reference0
	--
	if reference1 == "firstboot" //This is the end of the firstboot dialogue
	{
		"\0\s[0]Sounds good!\w8\w8 To access the test variable or the to-do list,\w4 you can open script input with ctrl + s and type \%\(debug = 1)\w8\w8\n\nYou can also check if there's a newer version of the template!\w8\w8 Have fun!\e"
	}
	else //This is if the user is changing their pronouns through the menu
	{
		"\0\s[0]Ok,\w4 I'll use %(heshe) %(himher) and %(hisher)."
		--
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
}
//Pronoun examples for my own sake:
//"%(heshe) left that there."
//"%(hisher) work is really cool."
//"I'll ask %(himher) later."
//"%(hesshes) really good at it."

OnChangeBirthday //Pulled a lot of this from the GT template, but I also modified it a lot
{
	"\0\s[0]Sure!\w8\w8 What month were you born in?\w8\w8 Name or number."
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\*\e"
}

//birthday[0] - word birthday
//birthday[1] - number birthday
//Use birthday[0] to display the user's birthday, and birthday[1] to do checks with the numbers
OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	_month = 0
	
	if _tempmonth == "january" || _tempmonth == "1"; _month = 1
	elseif _tempmonth == "february" || _tempmonth == "2"; _month = 2
	elseif _tempmonth == "march" || _tempmonth == "3"; _month = 3
	elseif _tempmonth == "april" || _tempmonth == "4"; _month = 4
	elseif _tempmonth == "may" || _tempmonth == "5"; _month = 5
	elseif _tempmonth == "june" || _tempmonth == "6"; _month = 6
	elseif _tempmonth == "july" || _tempmonth == "7"; _month = 7
	elseif _tempmonth == "august" || _tempmonth == "8"; _month = 8
	elseif _tempmonth == "september" || _tempmonth == "9"; _month = 9
	elseif _tempmonth == "october" || _tempmonth == "10"; _month = 10
	elseif _tempmonth == "november" || _tempmonth == "11"; _month = 11
	elseif _tempmonth == "december" || _tempmonth == "12"; _month = 12
	else //if the month isn't a valid month, like 32 or something.
	{
		"\0\s[0]%(_tempmonth) isn't a valid month,\w4 try again."
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\*\e"
	}
	--
	//If it's a valid month
	"%(Capitalize(MonthName(_month))) huh?\w8\w8 Ok,\w4 what day?"
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1,--reference=%(_month)]\*\e"
}

OnUserBornDay
{
	
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	_month = reference2 //Just for readability
	
	if IsValidDay(_month,_tempday)
	{
		birthday[0] = "%(Capitalize(MonthName(_month))) %(NumSuffix(_tempday))"
		birthday[1] = "%(_month)/%(_tempday)" //If you want to write dates as DD/MM, feel free to swap this around
		
		if birthday[1] == "%(month)/%(day)" //if today is the user's birthday. Don't forget to switch this if you change the format to DD/MM
		{
			"\0\s[0]Happy birthday,\w4 %(username)!"
		}
		elseif birthday[1] == "12/25" //If it's on christmas. Put any date you'd like here. If you change the format to DD/MM, make sure you change any dates like this that might be in bootend.
		{
			"\0\s[0]Oh,\w4 you were born on Christmas?\w8\w8 Cool!"
		}
		else //if the birthday is not listed above.
		{
			"\0\s[0]Ok, your birthday is %(birthday[0]).\w8\w8 I'll remember."
		}
		--
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //If it isn't a valid day, we open the input box again and have a \e so the dialogue below doesn't play.
	{
		"\0\s[0]That isn't a valid day.\w8\w8 Try again."
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1]\*\e"
	}
	
}